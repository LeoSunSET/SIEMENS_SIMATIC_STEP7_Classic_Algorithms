FUNCTION "EXP_T_ms" : VOID
TITLE =EXTENDED_PULSE_TIMER_MSEC

//################################################################################
//#                          EXTENDED_PULSE_TIMER_MSEC                           #
//################################################################################
//
//Revisions history
//+--------------------------------------------------------------------------+
//! 2 !        !                               !              !              !
//+---+--------+-------------------------------+--------------+--------------!
//! 1 !19/09/21!ISSUE                          ! Leonardo     !              !
//+---+--------+-------------------------------+--------------+--------------!
//!Rev!Date    !Description                    !Drawn         !Checked       !
//+--------------------------------------------------------------------------+
//
//Function         : EXTENDED PULSE timer, with 1-millisecond time base.
//
//Calling block    : ANY
//
//Used DB          : 
//                     
//Used Flags       : 
// 
//Used prog.block  : 
//
//Remarks          :

AUTHOR : LEONARDO
FAMILY : TIMING
NAME : SE_T_MS
VERSION : 1.0


VAR_INPUT
  START : BOOL ;	        //Timer Start
  PRESET : DINT ;	        //Timer Preset Value (milliseconds)
  OB_TIME_MS : INT ;	    //Time since last OB scan (milliseconds)
END_VAR
VAR_IN_OUT
  ACCUM : DINT ;	        //Time Accumulator (milliseconds)
END_VAR
BEGIN
NETWORK
TITLE =

      A     #START;         // If the Command Bit is OFF and the
      JC    TIMI;           // time count is zero (time expired),
      A(    ;               // force the time accumulator to +2^31
      L     L#0;            // and exit with boolean status = OFF.
      L     #ACCUM; 
      ==D   ; 
      )     ; 
      O(    ; 
      L     L#2147483647; 
      ==D   ; 
      )     ; 
      JCN   TIMI; 
      CLR   ; 
      SAVE  ; 
      L     L#2147483647; 
      T     #ACCUM; 
      BEU   ; 
TIMI: L     #ACCUM;         // If the Accumulator is > Preset,
      L     #PRESET;        // force it to the Preset Value
      >D    ; 
      JCN   DECR; 
      T     #ACCUM;         // Then decrease the Accumulator
DECR: L     #ACCUM;         // Decrease the time accumulator by the
      L     #OB_TIME_MS;    // milliseconds since last OB1 call
      -D    ; 
      T     #ACCUM;         // The BR is TRUE if the accumulated
      L     L#0;            // value is >0, otherwise force the 
      >D    ;               // accumulator to ZERO to prevent
      =     L      0.0;     // further accumulation that could result
      JC    OUTP;           // in rollover of the 32-bit accumulator  
      T     #ACCUM;         // and consequent wrong timer output.
OUTP: A     L      0.0;     // The FC output is true when the count
      SAVE  ;               // is ON.

END_FUNCTION
