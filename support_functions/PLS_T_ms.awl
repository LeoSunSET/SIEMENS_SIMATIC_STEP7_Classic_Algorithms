FUNCTION "PLS_T_ms" : VOID
TITLE =PULSE_TIMER_MSEC

//################################################################################
//#                               PULSE_TIMER_MSEC                               #
//################################################################################
//
//Revisions history
//+--------------------------------------------------------------------------+
//! 2 !        !                               !              !              !
//+---+--------+-------------------------------+--------------+--------------!
//! 1 !19/09/21!ISSUE                          ! Leonardo     !              !
//+---+--------+-------------------------------+--------------+--------------!
//!Rev!Date    !Description                    !Drawn         !Checked       !
//+--------------------------------------------------------------------------+
//
//Function         : PULSE timer, with 1-millisecond time base.
//
//Calling block    : ANY
//
//Used DB          : 
//                     
//Used Flags       : 
// 
//Used prog.block  : 
//
//Remarks          :

AUTHOR : LEONARDO
FAMILY : TIMING
NAME : SP_T_MS
VERSION : 1.0


VAR_INPUT
  START : BOOL ;	        //Timer Start
  PRESET : DINT ;	        //Timer Preset Value (milliseconds)
  OB_TIME_MS : INT ;	    //Time since last OB scan (milliseconds)
END_VAR
VAR_IN_OUT
  ACCUM : DINT ;	        //Time Accumulator (milliseconds)
END_VAR
BEGIN
NETWORK
TITLE =

      A     #START;         // If the Command Bit is OFF, force the
      JC    C_ON;           // time accumulator to the MAx DINT value  
      CLR   ;               // and exit with boolean status = OFF.
      SAVE  ; 
      L     L#2147483647; 
      T     #ACCUM; 
      BEU   ; 
C_ON: L     #ACCUM; 
      L     #PRESET; 
      >D    ; 
      JCN   TIMI; 
      T     #ACCUM; 
TIMI: L     #ACCUM;         // Decrease the time accumulator by the
      L     #OB_TIME_MS;    // milliseconds since last OB1 call
      -D    ; 
      T     #ACCUM;         // The BR is TRUE if the accumulated
      L     L#0;            // value is >0, otherwise force the 
      >D    ;               // accumulator to ZERO to prevent
      =     L      0.0;     // further accumulation that could result
      JC    OUTP;           // in rollover of the 16-bit accumulator  
      T     #ACCUM;         // and consequent wrong timer output.
OUTP: A     L      0.0;     // The FC output is true when the count
      A     #START;         // has not reached zero and the input
      SAVE  ;               // is ON.

END_FUNCTION
